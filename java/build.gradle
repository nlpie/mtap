/*
 * Copyright 2019 Regents of the University of Minnesota.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.10'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    def pattern = ~/v([0-9]+)(?:.([0-9]+))?(?:.([0-9]+))?(?:-(alpha|beta|pre|rc).([0-9]+))?(?:-([0-9]+)(?:-([a-g0-9]+))(-dirty)?)/
    def newVersion = stdout.toString().trim().replaceFirst(pattern) { _,major,minor,patch,pre,preVersion,com,hash,dirty ->
        def incremented = false
        def result = ''
        if (com != null || hash != null || dirty != null) {
            result = '-SNAPSHOT'
        }
        if (pre != null && preVersion != null) {
            if (!incremented) {
                preVersion = (preVersion as int) + 1
                incremented = true
            }
            result = "-${pre}.${preVersion}${result}"
        }
        if (patch != null) {
            if (!incremented) {
                patch = (patch as int) + 1
                incremented = true
            }
            result = ".${patch}${result}"
        }
        if (minor != null) {
            if (!incremented) {
                minor = (minor as int) + 1
                incremented = true
            }
            result = ".${minor}${result}"
        }
        if (!incremented) {
            major = (major as int) + 1
        }
        result = "${major}${result}"
        return result
    }
    return newVersion
}

group = 'edu.umn.nlpie'
version gitVersion()

compileJava {
    targetCompatibility '1.8'
    sourceCompatibility '1.8'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    mavenLocal()
}

def grpcVersion = '1.25.0'
def protobufVersion = '3.11.1'
def protocVersion = protobufVersion
def junitVersion = '5.5.2'
def sl4fjVersion = '1.7.29'

java {
    registerFeature('log4j2slf4jBinding') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    api group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    api group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: '1.17.0'
    api group: 'args4j', name: 'args4j', version: '2.33'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: "com.google.guava", name: "guava", version:   "28.1-jre"
    implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

    implementation group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.25'
    implementation group: 'com.orbitz.consul', name: 'consul-client', version: '1.3.9'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: sl4fjVersion

    implementation group: 'io.grpc', name: 'grpc-services', version: grpcVersion

    log4j2slf4jBindingImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.12.1'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.0'
    testImplementation group: 'io.grpc', name: 'grpc-testing', version: grpcVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: sl4fjVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

javadoc {
    exclude "edu/umn/nlpie/mtap/api/**"
    exclude "grpc"
}

sourceSets {
    main {
        proto {
            srcDir '../proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform()
}

task printVersion() {
    printf(version)
}

task fatJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'MTAP',
        	'Implementation-Version': archiveVersion
    }
    archiveBaseName.set(project.name + '-all')
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'nlpie'
                        name = 'NLP/IE Group'
                        email = 'nlp-ie@umn.edu'
                    }
                    developer {
                        id = 'benknoll_umn'
                        name = 'Ben Knoll'
                        email = 'benknoll@umn.edu'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/nlpie/mtap.git'
                    developerConnection = 'scm:git:https://github.com/nlpie/mtap.git'
                    url = 'http://github.com/nlpie/mtap'
                }

            }
        }
    }
}


task execute(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = project.hasProperty('mainClass') ? project.getProperties().get('mainClass') : null
}