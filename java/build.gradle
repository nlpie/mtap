plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.13'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.jfrog.bintray' version '1.8.5'
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    def pattern = ~/v([0-9]+)(?:.([0-9]+))?(?:.([0-9]+))?(?:-(alpha|beta|pre|rc).([0-9]+))?(?:-([0-9]+)-([a-g0-9]+))?(-dirty)?/
    def newVersion = stdout.toString().trim().replaceFirst(pattern) { _,major,minor,patch,pre,preVersion,com,hash,dirty ->
        def incremented = false
        def result = ''
        if (com != null || hash != null || dirty != null) {
            result = '-SNAPSHOT'
        } else {
            incremented = true
        }
        if (pre != null && preVersion != null) {
            if (!incremented) {
                preVersion = (preVersion as int) + 1
                incremented = true
            }
            result = "-${pre}${preVersion}${result}"
        }
        if (patch != null) {
            if (!incremented) {
                patch = (patch as int) + 1
                incremented = true
            }
            result = ".${patch}${result}"
        }
        if (minor != null) {
            if (!incremented) {
                minor = (minor as int) + 1
                incremented = true
            }
            result = ".${minor}${result}"
        }
        if (!incremented) {
            major = (major as int) + 1
        }
        result = "${major}${result}"
        return result
    }
    return newVersion
}

group = 'edu.umn.nlpie'
version gitVersion()
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    jcenter()
    mavenLocal()
}

def grpcVersion = '1.29.0'
def protobufVersion = '3.11.4'
def protocVersion = protobufVersion
def junitVersion = '5.6.2'
def sl4fjVersion = '1.7.30'

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    api group: 'org.jetbrains', name: 'annotations', version: '19.0.0'
    api group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: '1.18.0'
    api group: 'args4j', name: 'args4j', version: '2.33'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
    implementation group: 'com.orbitz.consul', name: 'consul-client', version: '1.4.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: sl4fjVersion

    implementation group: 'io.grpc', name: 'grpc-services', version: grpcVersion

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    testImplementation group: 'io.grpc', name: 'grpc-testing', version: grpcVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: sl4fjVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

sourceSets {
    main {
        proto {
            srcDir '../proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform()
}

task printVersion() {
    printf(version)
}

shadowJar {
    manifest {
        attributes 'Implementation-Title': 'MTAP',
                'Implementation-Version': archiveVersion
    }
    mergeServiceFiles()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifacts = [jar, sourcesJar, javadocJar]
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Microservice Text Analysis Platform'
                description = 'A framework for distributed text analysis using gRPC and ' +
                        'microservices-based architecture.'
                url = 'https://nlpie.github.io/mtap'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'nlpie'
                        name = 'NLP/IE Group'
                        email = 'nlp-ie@umn.edu'
                    }
                    developer {
                        id = 'benknoll_umn'
                        name = 'Ben Knoll'
                        email = 'benknoll@umn.edu'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/nlpie/mtap.git'
                    developerConnection = 'scm:git:https://github.com/nlpie/mtap.git'
                    url = 'http://github.com/nlpie/mtap'
                }

            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.hasProperty('sonatypeUsername') ? sonatypeUsername : null
                password project.hasProperty('sonatypePassword') ? sonatypePassword : null
            }
        }
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.key')
    pkg {
        repo = 'mtap'
        name = 'mtap'
        userOrg = 'benknoll-umn'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nlpie/mtap.git'
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    exclude "edu/umn/nlpie/mtap/api/**"
    exclude "grpc"
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

task execute(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = project.hasProperty('mainClass') ? project.getProperties().get('mainClass') : null
}
