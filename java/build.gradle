/*
 * Copyright 2019 Regents of the University of Minnesota.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.8'
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().replaceFirst('^v', "").trim()
}

version gitVersion()

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}

def grpcVersion = '1.20.0'
def nettyTcNativeVersion = '2.0.22.Final'
def protobufVersion = '3.7.1'
def protocVersion = '3.7.1'
def junitVersion = '5.3.1'

dependencies {
    implementation group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: '1.0.0'
    implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
    implementation group: 'args4j', name: 'args4j', version: '2.33'

    implementation group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.24'
    implementation group: 'org.jetbrains', name: 'annotations', version: '17.0.0'
    implementation group: 'com.orbitz.consul', name: 'consul-client', version: '1.3.2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'

    implementation group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-services', version: grpcVersion
    implementation group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: nettyTcNativeVersion

    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'
    testImplementation group: 'io.grpc', name: 'grpc-testing', version: grpcVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

javadoc {
    exclude "edu/umn/nlpnewt/api/**"
    exclude "edu/umn/nlpnewt/internal/**"
    exclude "grpc"
}

sourceSets {
    main {
        proto {
            srcDir '../proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform()
}

startScripts.enabled = false

task newt(type: JavaExec) {
    main = 'edu.umn.nlpnewt.Newt'
    classpath = sourceSets.main.runtimeClasspath
}

task printVersion() {
    printf(version)
}

task fatJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'NLP-NEWT',
        	'Implementation-Version': version,
        	'Main-Class': 'edu.umn.nlpnewt.Newt'
    }
    baseName = project.name + '-all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
